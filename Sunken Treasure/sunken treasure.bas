1 REM ******************************
2 REM **      SUNKEN TREASURE     **
3 REM **      ADVENTURE  GAME     **
4 REM ** BY SCOTT JULIAN (c) 1985 **
5 REM ******************************
6 CLR:POKE53280,0:POKE53281,0:PRINTCHR$(147)
7 PRINTTAB(12)CHR$(154)"SUNKEN TREASURE"
8 PRINTCHR$(17)CHR$(17)TAB(2)"A BASIC ADVENTURE FOR THE COMMODORE 64"
9 PRINTCHR$(17)TAB(12)"BY SCOTT JULIAN"
10 V=53248:POKEV+21,4:POKE2042,13
11 POKE23,4:POKEV+29,4
12 POKEV+41,13
13 POKEV+23,13
14 FORN=0TO62:READQ:POKE832+N,Q:NEXT
15 POKEV+5,120
16 POKEV+4,160
18 POKEV+4,160
22 FORT=1TO3000:NEXTT:POKEV+21,0:GOTO100
25 DATA0,0,0
27 DATA0,0,0
30 DATA0,0,0
32 DATA3,255,192
35 DATA5,0,160
37 DATA9,0,144
40 DATA17,0,136
42 DATA17,0,136
45 DATA31,255,248
47 DATA18,36,72
50 DATA18,36,72
52 DATA18,24,72
55 DATA18,24,72
57 DATA18,0,72
60 DATA18,0,72
62 DATA18,0,72
65 DATA31,255,248
67 DATA0,0,0
70 DATA0,0,0
72 DATA0,0,0
75 DATA0,0,0
100 W=5:G=5:V=21:RM=1:Z=1:E=1:CH=0:DO=0:D=0:OP=0:OPE=0:DD=0
110 GOSUB2000
120 PRINTCHR$(147)
130 PRINTCHR$(154)"YOU ARE ";D$(RM)
132 PRINTCHR$(17)"YOU SEE ";OB$(Z);"."
133 FORI=1TOG
135 IFL(I)=RMANDF(I)=1THENPRINTCHR$(17)"YOU ALSO SEE A ";O$(I);"."
137 NEXTI
140 PRINTCHR$(17)"VISIBLE EXITS:";R$(E)
145 PRINTCHR$(17):FORT=1TO40:PRINTCHR$(156)"*";:NEXTT
147 REM *** INPUT SECTION ***
150 PRINTCHR$(17)CHR$(158)M$:M$="WHAT"
153 IFQW=1THENFORA=1TO999:NEXTA:RETURN
154 IFWQ=1THENFORA=1TO999:NEXTA:RETURN
155 PRINTCHR$(153)CHR$(17)"WHAT SHALL I DO NOW";:INPUTQ$
160 V$="":W$="":VB=0:OB=0
170 FORI=1TOLEN(Q$)
175 IFMID$(Q$,I,1)=" "ANDV$=""THENV$=LEFT$(Q$,I-1)
180 IFMID$(Q$,I+1,1)<>" "ANDV$<>""THENW$=MID$(Q$,I+1,LEN(Q$)-1):I=LEN(Q$)
190 NEXTI
200 IFW$=""THENV$=Q$
210 FORI=1TOV
220 IFV$=V$(I)THENVB=I
230 NEXTI
240 FORI=1TOW
250 IFW$=O$(I)THENLETOB=I
255 NEXTI
260 REM *** MESSAGES ***
265 IFW$>""ANDOB=0THENM$="THAT'S SILLY"
275 IFVB>VANDOB>0THENM$="YOU DON'T MAKE SENCE"
290 IFVB<VANDOB>0ANDC(OB)=0THENM$="YOU DON'T HAVE A '"+W$+"'"
295 REM ** BRANCH TO SUBROUTINES **
300 IFVB>14THEN330
310 ONVBGOSUB400,450,500,500,550,550,600,700,760,800,860,900,960,980
320 GOTO340
330 ONVB-14GOSUB1000,1100,1200,1300,1330,1340,1400
340 IFUN>0ANDUN<10THENM$="YOU HAVEN'T MUCH AIR IN YOUR LUNGS."
345 IFUN=10THEN3000
350 M$="":GOTO120
400 REM *** HELP ROUTINE ***
410 PRINTCHR$(147)CHR$(156)"WORDS I KNOW"CHR$(5)CHR$(17)
412 FORY=1TO1500:NEXTY:PRINTCHR$(147)
415 FORI=1TOV
420 PRINTV$(I);"."
425 NEXTI
430 PRINTCHR$(18)CHR$(17)"PRESS A KEY TO CONTINUE"
435 GETA$:IFA$=""THEN435
440 M$="":PRINT
445 RETURN
450 REM *** INVENTORY ROUTINE ***
455 PRINTCHR$(147)CHR$(158)CHR$(18)"YOU ARE CARRYING"CHR$(17)
460 FORI=1TOG
465 IFC(I)=1THENPRINTO$(I);"."CHR$(17)
470 NEXTI
475 M$="":PRINT
480 PRINTCHR$(17)"PRESS A KEY TO COUTINUE"
485 GETA$:IFA$=""THEN485
490 RETURN
500 REM *** GET & TAKE ROUTINE ***
505 IFOB>GTHENM$="I CAN'T GET A '"+W$+"'"
510 IFL(OB)<>RMTHENM$="IT ISN'T HERE."
520 IFF(OB)<>0THENM$="WHAT "+W$+"?"
525 IFC(OB)=1THENM$="YOU ALREADY HAVE IT."
530 IFOB>0ANDL(OB)=RMTHENC(OB)=1:L(OB)=65:M$="YOU HAVE THE '"+W$+"'"
540 RETURN
549 REM *** DROP & LEAVE ROUTINE **
550 IFC(OB)=1THENC(OB)=0:L(OB)=RM:M$="DONE"
555 IFC(2)=1ANDRM=4THEN5000
560 RETURN
599 REM *** GO ROUTINE ***
600 IFRM=1ANDW$="EAST"THENRM=3:E=3:Z=3:VB=0:RETURN
602 IFRM=1ANDW$="WEST"THENRM=6:E=6:Z=6:RETURN
604 IFRM=1ANDCHO=1ANDW$="SOUTH"THENRM=5:E=5:Z=5:RETURN
606 IFRM=3ANDW$="WEST"THENRM=1:E=1:Z=1:RETURN
608 IFRM=4ANDW$="SOUTH"THENRM=3:E=3:Z=3:RETURN
610 IFRM=5ANDW$="NORTH"THENRM=1:E=1:Z=1:RETURN
612 IFRM=6ANDW$="EAST"THENRM=1:E=1:Z=1:RETURN
614 IFRM=6ANDW$="NORTH"THENRM=7:E=7:Z=7:RETURN
616 IFRM=7ANDW$="SOUTH"THENRM=6:E=6:Z=6:RETURN
618 IFRM=8ANDW$="SOUTH"THENRM=9:E=9:Z=9:RETURN
620 IFRM=8ANDW$="EAST"THENRM=10:E=10:Z=10:GOTO4000
622 IFRM=9ANDW$="NORTH"THENRM=8:E=8:Z=8:RETURN
624 IFRM=10ANDW$="WEST"THENRM=8:E=8:Z=8:RETURN
626 IFRM=3ANDOP=1ANDW$="DOOR"THENRM=4:E=4:Z=4:RETURN
628 IFRM=4ANDW$="DOOR"THENRM=3:E=3:Z=3:RETURN
630 IFRM=10ANDW$="HOLE"THENRM=11:E=11:Z=11:RETURN
632 IFRM=11ANDW$="HOLE"THENRM=10:E=10:Z=10:RETURN
700 REM *** CLIMB COMMAND ***
710 IFRM=1ANDCH=0ANDW$="TREE"THENRM=2:E=2:Z=2:RETURN
715 IFRM=2ANDW$="TREE"THENRM=1:E=1:Z=1:RETURN
759 REM *** PULL COMMAND ***
760 IFPU=0ANDW$="NOTE"ANDRM=4ANDF(2)=0THENF(2)=1:NO=1
762 IFNO=1THENM$="PULLING THE NOTE FROM THE WALL REVEALS  SOMETHING.":QQ=1
764 IFQQ=1THENQW=1:GOSUB120:M$="":QW=0:QQ=0:NO=0:GOTO120
799 REM *** ROW COMMAND ***
800 IFRM=7ANDW$="BOAT"ANDRO=1THENRM=8:E=8:Z=8:RETURN
805 IFRM=8ANDW$="BOAT"ANDRO=1THENRM=7:E=7:Z=7:RETURN
810 M$="I CAN'T DO THAT YET!!":WQ=1:GOSUB120:M$="":WQ=0:GOTO120
860 M$="I CAN'T THROW ANYTHING YET!!":WQ=1:GOSUB120:M$="":WQ=0:GOTO120
900 REM *** UNLOCK COMMAND ***
905 IFRM=3ANDC(1)=1ANDDO=0THENDO=1:M$="O.K.":QW=1:GOSUB120:M$="":QW=0: GOTO120
910 IFRM=3ANDC(1)=1ANDDO<>0THENM$="IT IS UNLOCKED.":QW=1:GOSUB120:M$="":QW=0:GOTO120
915 IFRM=3ANDC(1)<>1THENM$="YOU HAVEN'T GOT A KEY.":QW=1:GOSUB120:M$="":QW=0:GOTO120
917 IFRM=11ANDC(3)=1ANDDD=0THEND=1:M$="O.K.":QW=1:GOSUB120:M$="":QW=0: GOTO120
918 IFRM=11ANDC(5)=1ANDD<>0THENM$="IT IS UNLOCKED.":QW=1:GOSUB120:M$="":QW=0:GOTO120
919 IFRM=11ANDC(5)<>1THENM$="YOU HAVEN'T GOT A KEY.":QW=1:GOSUB120:M$="":QW=0:GOTO120
920 IFRM=11ANDC(3)=1ANDD=0THEND=1:M$="O.K.":QW=1:GOSUB120:M$="":QW=0: GOTO120
925 IFRM=11ANDC(3)=1ANDD<>0THENM$="IT IS UNLOCKED.":QW=1:GOSUB120:M$="":QW=0:GOTO120
930 IFRM=11ANDC(3)<>1THENM$="YOU HAVEN'T GOT A KEY.":QW=1:GOSUB120:M$="":QW=0:GOTO120
935 M$="YOU CAN'T UNLOCK THAT!":QW=1:GOSUB120:M$="":QW=0:GOTO120
960 REM *** OPEN COMMAND ***
965 IFRM=3ANDDO=1ANDOP=0THENOP=1:M$="O.K.":QW=1:GOSUB120:M$="":QW=0:GOTO120
970 IFRM=3ANDOP=1THENM$="IT'S OPEN.":QW=1:GOSUB120:QW=0:M$="":GOTO120
975 IFRM=11ANDD=1THEND=2:M$="O.K.":QW=1:GOSUB120:M$="":QW=0:F(5)=1:GOTO120
977 IFRM=11ANDOPE=1THENM$="IT'S OPEN.":QW=1:GOSUB120:M$="":QW=0:GOTO120
979 M$="YOU CAN'T OPEN THAT!!":QW=1:GOSUB120:M$="":QW=0:GOTO120
980 REM *** MOVE COMMAND ***
982 IFRM=7ANDSEA=0ANDW$="SEAWEED"THENOB$(7)="ROCKS.SEAWEED.ROW BOAT.":RO=1:A=1
984 IFA=1THENM$="MOVING THE SEAWEED REVEALS A ROW BOAT":QW=1:GOSUB120:M$="":QW=0:AA=1
986 IFAA=1THENA=0:GOTO120
988 M$="YOU CAN'T MOVE THAT":QW=1:GOSUB120:M$="":QW=0:GOTO120
1000 REM *** QUIT COMMAND ***
1002 PRINTCHR$(147)"ARE YOU SURE (Y/N)";:INPUTA$
1004 IFA$="Y"THENEND
1006 RETURN
1100 REM *** SAVE COMMAND ***
1110 PRINTCHR$(147)"ENTER BLANK CASSETTE"
1112 PRINTCHR$(17)"PRESS ANY KEY WHEN READY."
1114 GETA$:IFA$=""THEN1114
1116 OPEN1,1,1,"SAVED GAME"
1118 PRINT#1,RM
1120 PRINT#1,T
1122 PRINT#1,Z
1124 PRINT#1,E
1126 FORI=1TOG
1128 PRINT#1,L(I)
1130 NEXTI
1132 FORI=1TOW
1134 PRINT#1,C(I),F(I)
1136 NEXTI
1138 CLOSE1
1140 RETURN
1200 REM *** LOAD COMMAND ***
1210 PRINTCHR$(147)"ENTER SAVED GAME TAPE"
1220 PRINTCHR$(17)"PRESS ANY KEY WHEN READY"
1225 GETA$:IFA$=""THEN1225
1230 OPEN1,1,0,"SAVED GAME"
1235 INPUT#1,RM
1237 INPUR#1,T
1240 INPUT#1,Z
1243 INPUT#1,E
1245 FORI=1TOG
1250 INPUT#1,L(I)
1255 NEXTI
1260 FORI=1TOW
1265 INPUT#1,C(I),F(I)
1270 NEXTI
1275 CLOSE1
1280 RETURN
1300 REM *** CHOP COMMAND ***
1310 IFRM=1ANDC(2)=1ANDW$="TREE"ANDCH=0THENDH=1:OB$(1)="STUMP.LAGOON.":CH=1
1315 IFDH=1THEND$(1)="BESIDE A HOLLOW STUMP. THERE IS THE REMAINS OF A FALLEN TREE"
1317 IFDH=1THENR$(1)="EAST.WEST.SOUTH.":DH=0
1320 IFRM<>1THENM$="I CAN'T DO THAT HERE.":QW=1:GOSUB120:M$="":QW=0:GOTO120
1325 M$="":QW=0:GOTO120
1330 REM *** RESTART COMMAND ***
1332 FORTT=1TO1000:NEXTTT:CLR:RUN
1340 REM *** LOOK COMMAND ***
1345 RETURN
1400 REM *** READ COMMAND ***
1402 IFRM=4ANDW$="NOTE"THENM$="DROP TREASURE HERE!!!":QW=1:GOSUB120:M$="":QW=0:GOTO120
1404 M$="I CAN'T READ HERE.":QW=1:GOSUB120:QW=0:M$="":GOTO120
2000 REM **** DATA ****
2010 DIMR$(11),D$(11),O$(W),OB$(11),V$(V)
2020 DIMC(W),L(G),F(W)
2030 DATA2,4,5,9,11
2040 FORI=1TOG
2050 READL(I)
2060 NEXTI
2070 DATAHELP,INV,GET,TAKE,DROP,LEAVE,GO,CLIMB,PULL,ROW,THROW,UNLOCK,OPEN,MOVE
2080 DATAQUIT,SAVE,LOAD,CHOP,RESTART,LOOK,READ
2090 FORI=1TOV
2100 READV$(I)
2110 NEXTI
2120 DATAEAST.WEST,NONE,WEST,SOUTH,NORTH,EAST.NORTH,SOUTH,SOUTH.EAST,NORTH
2125 DATAWEST,NONE
2130 FORI=1TO11
2140 READR$(I)
2150 NEXTI
2160 DATABESIDE A VERY TALL TREE.THERE IS A LAGOON HERE.
2170 DATAAT THE TOP OF A TALL TREE.THERE ARE BRANCHES HERE.
2180 DATAOUTSIDE A HUT.THERE IS A DOOR IN FRONT OF YOU.
2190 DATAINSIDE THE HUT.THERE IS A BRICK WALL WITH A NOTE STUCK TO IT.
2200 DATAINSIDE A HOLLOW STUMP.THERE IS  SAND ON THE FLOOR.
2210 DATAON A SANDY BEACH.OUT TO SEA     THERE IS A SHIPWREAK
2220 DATAAT THE END OF THE BEACH.THERE   ARE ROCKS AND SEAWEED HERE.
2230 DATA IN A BOAT BESIDE A CROWSNEST.   BELOW YOU IS THE SHIPWREAK.
2240 DATAINSIDE THE CROWSNEST.
2250 DATABESIDE THE SHIPWREAK.
2260 DATAINSIDE THE SHIPWREAK.THERE IS A CHEST HERE.
2270 FORI=1TO11
2280 READD$(I)
2290 NEXTI
2300 DATABRASS KEY,SHINEY AXE,SKELETON KEY,SPEAR,TREASURE
2310 FORI=1TOW
2320 READO$(I)
2330 NEXTI
2340 DATATREE.LAGOON,BRANCHES.WEB,HUT.DOOR,WALL.DOOR,SAND,SAND.SHIPWREAK
2350 DATAROCKS.SEAWEED,CROWSNEST.SHIPWREAK,SPIDER WEBS,SHIPWREAK.HOLE,CHEST.HOLE
2360 FORI=1TO11
2370 READOB$(I)
2380 NEXTI
2390 F(1)=1:F(2)=0:F(3)=1:F(4)=1:F(5)=0
2400 RETURN
4000 REM **** MONSTER ****
4005 IFMO=1THEN120
4010 PRINTCHR$(147)
4020 PRINT"THERE IS A SEA MONSTER ATACKING "
4030 PRINT:PRINT:PRINT
4035 PRINT"WHAT SHALL I DO NOW";:INPUTA$
4040 IFA$="THROW SPEAR"ANDC(4)=1THEN4200
4050 PRINT:PRINT"THE MONSTER EATS YOU!!!!!"
4055 PRINT:PRINT"THAT IS THE END OF THE GAME"
4060 PRINT:PRINT"DO YOU WISH TO PLAY AGAIN(Y/N)";:INPUTA$
4065 IFA$="Y"THENCLR:RUN
4070 END
4200 PRINT:PRINT"YOU HAVE JUST KILLED THE MONSTER.."
4210 LETMO=1:M$="":PRINT:PRINT"PRESS ANY KEY TO CONTINUE"
4220 GETA$:IFA$=""THEN4220
4225 GOTO120
5000 REM *** END OF GAME ***
5010 PRINTCHR$(147)TAB(14)"WELL DONE"
5020 PRINTCHR$(17)TAB(3)"YOU HAVE COMPLETED SUNKEN TREASURE"
5030 PRINTCHR$(17)TAB(11)"ADVENTURE  GAME."
5100 VO=54296:AD=54277:SR=54278:WF=54276:HF=54273:LF=54272:HP=54275:    LP=54274
5120 POKEAD,17:POKESR,132:POKEWF,33:POKEVO,15
5130 H1=5:L1=10
5140 POKEHF,H1:POKELF,L1
5150 FORTD=1TO2:NEXT
5160 H1=H1+5:L1=L1+10
5170 IFL1<250THENGOTO5140
5180 POKEVO,0:POKEWF,0
6000 PRINTCHR$(17)"DO YOU WISH TO PLAY AGAIN(Y/N)";:INPUTA$
6010 IFA$="Y"THENCLR:RUN
6020 END
